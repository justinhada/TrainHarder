plugins {
    id 'java-library'
    id 'war'
    id 'jacoco'
    id "org.sonarqube" version '3.0'
}

repositories {
    jcenter()
}

[
        "integrationstests",
        "tests",
        "slf4j",
].each {
    apply from: "gradle/${it}.gradle"
}


dependencies {
    api "org.apache.commons:commons-math3:3.6.1"

    implementation "org.projectlombok:lombok:1.18.12",
            "com.google.guava:guava:28.0-jre",
            "javax.xml.bind:jaxb-api:2.3.1",
            "org.primefaces:primefaces:7.0",
            "javax.servlet:javax.servlet-api:3.1.0",
            "javax:javaee-api:8.0",
            "javax.security.enterprise:javax.security.enterprise-api:1.0",
            "javax.mvc:javax.mvc-api:1.0.0",
            "org.mvc-spec.ozark:ozark-core:1.0.0-m04",
            "org.mvc-spec.ozark:ozark-resteasy:1.0.0-m04",
            "org.projectlombok:lombok:1.18.12",
            "javax.mail:javax.mail-api:1.6.2",
            "nl.jqno.equalsverifier:equalsverifier:3.4.3"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"

    integrationTestRuntimeOnly "org.hibernate:hibernate-core:5.4.12.Final",
            "org.mariadb.jdbc:mariadb-java-client:2.6.0"

    annotationProcessor "org.projectlombok:lombok:1.18.12"
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/jacoco")
}

task mergeJacocoReports(type: JacocoReport) {
    dependsOn test, integrationTest
    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/*.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/jacoco/merge")
        xml.destination file("${buildDir}/jacoco/merge.xml")
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "justinhada_TrainHarder"
        property "sonar.organization", "justinhada"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/jacoco/merge.xml"
    }
}

tasks['sonarqube'].dependsOn mergeJacocoReports

version = '0.0.2'
